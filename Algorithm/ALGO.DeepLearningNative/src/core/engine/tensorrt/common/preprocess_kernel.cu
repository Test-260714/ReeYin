
#include "preprocess_kernel.cuh"

namespace CUDAKernel{

	Norm Norm::mean_std(const float mean[3], const float std[3], float alpha, ChannelType channel_type)
	{
		Norm out;
		out.type  = NormType::MeanStd;
		out.alpha = alpha;
		out.channel_type = channel_type;
		memcpy(out.mean, mean, sizeof(out.mean));
		memcpy(out.std,  std,  sizeof(out.std));
		return out;
	}

	Norm Norm::alpha_beta(float alpha, float beta, ChannelType channel_type)
	{
		Norm out;
		out.type = NormType::AlphaBeta;
		out.alpha = alpha;
		out.beta = beta;
		out.channel_type = channel_type;
		return out;
	}

	Norm Norm::None(){
		return Norm();
	}	

	#define INTER_RESIZE_COEF_BITS 11
	#define INTER_RESIZE_COEF_SCALE (1 << INTER_RESIZE_COEF_BITS)
	#define CAST_BITS (INTER_RESIZE_COEF_BITS << 1)
	template<typename _T>
	static __inline__ __device__ _T limit(_T value, _T low, _T high){
		return value < low ? low : (value > high ? high : value);
	}

	static __inline__ __device__ int resize_cast(int value){
		return (value + (1 << (CAST_BITS - 1))) >> CAST_BITS;
	}

	// same to opencv
	// reference: https://github.com/opencv/opencv/blob/24fcb7f8131f707717a9f1871b17d95e7cf519ee/modules/imgproc/src/resize.cpp
	// reference: https://github.com/openppl-public/ppl.cv/blob/04ef4ca48262601b99f1bb918dcd005311f331da/src/ppl/cv/cuda/resize.cu
	/*
	  可以考虑用同样实现的resize函数进行训练，python代码在：tools/test_resize.py
	*/
	__global__ void resize_bilinear_and_normalize_kernel(
		uint8_t* src, int src_line_size, int src_width, int src_height, float* dst, int dst_width, int dst_height, 
		float sx, float sy, Norm norm, int edge
	){
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge) return;

		int dx      = position % dst_width;
		int dy      = position / dst_width;
		float src_x = (dx + 0.5f) * sx - 0.5f;
		float src_y = (dy + 0.5f) * sy - 0.5f;
		float c0, c1, c2;

		int y_low = floorf(src_y);
		int x_low = floorf(src_x);
		int y_high = limit(y_low + 1, 0, src_height - 1);
		int x_high = limit(x_low + 1, 0, src_width - 1);
		y_low = limit(y_low, 0, src_height - 1);
		x_low = limit(x_low, 0, src_width - 1);

		int ly    = rint((src_y - y_low) * INTER_RESIZE_COEF_SCALE);
		int lx    = rint((src_x - x_low) * INTER_RESIZE_COEF_SCALE);
		int hy    = INTER_RESIZE_COEF_SCALE - ly;
		int hx    = INTER_RESIZE_COEF_SCALE - lx;
		int w1    = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;
		float* pdst = dst + dy * dst_width + dx * 3;
		uint8_t* v1 = src + y_low * src_line_size + x_low * 3;
		uint8_t* v2 = src + y_low * src_line_size + x_high * 3;
		uint8_t* v3 = src + y_high * src_line_size + x_low * 3;
		uint8_t* v4 = src + y_high * src_line_size + x_high * 3;

		c0 = resize_cast(w1 * v1[0] + w2 * v2[0] + w3 * v3[0] + w4 * v4[0]);
		c1 = resize_cast(w1 * v1[1] + w2 * v2[1] + w3 * v3[1] + w4 * v4[1]);
		c2 = resize_cast(w1 * v1[2] + w2 * v2[2] + w3 * v3[2] + w4 * v4[2]);

		if(norm.channel_type == ChannelType::Invert){
			float t = c2;
			c2 = c0;  c0 = t;
		}

		if(norm.type == NormType::MeanStd){
			c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
			c1 = (c1 * norm.alpha - norm.mean[1]) / norm.std[1];
			c2 = (c2 * norm.alpha - norm.mean[2]) / norm.std[2];
		}else if(norm.type == NormType::AlphaBeta){
			c0 = c0 * norm.alpha + norm.beta;
			c1 = c1 * norm.alpha + norm.beta;
			c2 = c2 * norm.alpha + norm.beta;
		}

		int area = dst_width * dst_height;
		float* pdst_c0 = dst + dy * dst_width + dx;
		float* pdst_c1 = pdst_c0 + area;
		float* pdst_c2 = pdst_c1 + area;
		*pdst_c0 = c0;
		*pdst_c1 = c1;
		*pdst_c2 = c2;
	}


	__global__ void warp_affine_bilinear_and_normalize_plane_general_kernel(float* src, int src_line_size, int src_width, int src_height,
		float* dst, int dst_width, int dst_height, float const_value_st, float* warp_affine_matrix_2_3, Norm norm, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge) return;

		float m_x1 = warp_affine_matrix_2_3[0];
		float m_y1 = warp_affine_matrix_2_3[1];
		float m_z1 = warp_affine_matrix_2_3[2];
		float m_x2 = warp_affine_matrix_2_3[3];
		float m_y2 = warp_affine_matrix_2_3[4];
		float m_z2 = warp_affine_matrix_2_3[5];

		int dx = position % dst_width;
		int dy = position / dst_width;
		float src_x = m_x1 * dx + m_y1 * dy + m_z1;
		float src_y = m_x2 * dx + m_y2 * dy + m_z2;

		int channel = (int)(src_line_size / src_width);
		
		if (channel == 3)
		{
			float c0, c1, c2;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
				c1 = const_value_st;
				c2 = const_value_st;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float const_value[] = { const_value_st, const_value_st, const_value_st };
				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;
				float* v1 = const_value;
				float* v2 = const_value;
				float* v3 = const_value;
				float* v4 = const_value;
				if (y_low >= 0)
				{
					if (x_low >= 0)
						v1 = src + y_low * src_line_size + x_low * 3;

					if (x_high < src_width)
						v2 = src + y_low * src_line_size + x_high * 3;
				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
						v3 = src + y_high * src_line_size + x_low * 3;

					if (x_high < src_width)
						v4 = src + y_high * src_line_size + x_high * 3;
				}

				c0 = w1 * v1[0] + w2 * v2[0] + w3 * v3[0] + w4 * v4[0];
				c1 = w1 * v1[1] + w2 * v2[1] + w3 * v3[1] + w4 * v4[1];
				c2 = w1 * v1[2] + w2 * v2[2] + w3 * v3[2] + w4 * v4[2];
			}

			if (norm.channel_type == ChannelType::Invert)
			{
				float t = c2;
				c2 = c0;  c0 = t;
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
				c1 = (c1 * norm.alpha - norm.mean[1]) / norm.std[1];
				c2 = (c2 * norm.alpha - norm.mean[2]) / norm.std[2];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
				c1 = c1 * norm.alpha + norm.beta;
				c2 = c2 * norm.alpha + norm.beta;
			}

			int area = dst_width * dst_height;
			float* pdst_c0 = dst + dy * dst_width + dx;
			float* pdst_c1 = pdst_c0 + area;
			float* pdst_c2 = pdst_c1 + area;
			*pdst_c0 = c0;
			*pdst_c1 = c1;
			*pdst_c2 = c2;
		}
		else
		{
			float c0;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float const_value[] = { const_value_st };
				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;
				float* v1 = const_value;
				float* v2 = const_value;
				float* v3 = const_value;
				float* v4 = const_value;
				if (y_low >= 0)
				{
					if (x_low >= 0)
						v1 = src + y_low * src_line_size + x_low * 1;

					if (x_high < src_width)
						v2 = src + y_low * src_line_size + x_high * 1;
				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
						v3 = src + y_high * src_line_size + x_low * 1;

					if (x_high < src_width)
						v4 = src + y_high * src_line_size + x_high * 1;
				}
				// same to opencv
				//c0 = floorf(w1 * v1[0] + w2 * v2[0] + w3 * v3[0] + w4 * v4[0] + 0.5f);
				c0 = w1 * v1[0] + w2 * v2[0] + w3 * v3[0] + w4 * v4[0];
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
			}

			float* pdst_c0 = dst + dy * dst_width + dx;
			*pdst_c0 = c0;
		}
	}


	__global__ void warp_affine_bilinear_and_normalize_image_mix_depth_kernel(float* src_image, int src_line_size, float* src_depth, int src_width, int src_height,
		float* dst, int dst_width, int dst_height, float const_value_st, float* warp_affine_matrix_2_3, Norm norm, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge) 
			return;

		float m_x1 = warp_affine_matrix_2_3[0];
		float m_y1 = warp_affine_matrix_2_3[1];
		float m_z1 = warp_affine_matrix_2_3[2];
		float m_x2 = warp_affine_matrix_2_3[3];
		float m_y2 = warp_affine_matrix_2_3[4];
		float m_z2 = warp_affine_matrix_2_3[5];

		int dx = position % dst_width;
		int dy = position / dst_width;
		float src_x = m_x1 * dx + m_y1 * dy + m_z1;
		float src_y = m_x2 * dx + m_y2 * dy + m_z2;

		int channel = (int)(src_line_size / src_width);

		if (channel == 3)
		{
			float c0, c1, c2, c4;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
				c1 = const_value_st;
				c2 = const_value_st;
				c4 = 0;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float image_const_value[] = { const_value_st, const_value_st, const_value_st };
				float depth_const_value[] = { 0 };

				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;

				float* iv1 = image_const_value;
				float* iv2 = image_const_value;
				float* iv3 = image_const_value;
				float* iv4 = image_const_value;
				float* dv1 = depth_const_value;
				float* dv2 = depth_const_value;
				float* dv3 = depth_const_value;
				float* dv4 = depth_const_value;

				if (y_low >= 0)
				{
					if (x_low >= 0)
					{
						iv1 = src_image + y_low * src_line_size + x_low * 3;
                        dv1 = src_depth + y_low * src_width + x_low * 1;
					}

					if (x_high < src_width)
					{
						iv2 = src_image + y_low * src_line_size + x_high * 3;
                        dv2 = src_depth + y_low * src_width + x_high * 1;
					}
						
				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
					{
						iv3 = src_image + y_high * src_line_size + x_low * 3;
                        dv3 = src_depth + y_high * src_width + x_low * 1;
					}
					
					if (x_high < src_width)
					{
						iv4 = src_image + y_high * src_line_size + x_high * 3;
                        dv4 = src_depth + y_high * src_width + x_high * 1;
					}
				}

				c0 = w1 * iv1[0] + w2 * iv2[0] + w3 * iv3[0] + w4 * iv4[0];
				c1 = w1 * iv1[1] + w2 * iv2[1] + w3 * iv3[1] + w4 * iv4[1];
				c2 = w1 * iv1[2] + w2 * iv2[2] + w3 * iv3[2] + w4 * iv4[2];
                c4 = w1 * dv1[0] + w2 * dv2[0] + w3 * dv3[0] + w4 * dv4[0];
			}

			if (norm.channel_type == ChannelType::Invert)
			{
				float t = c2;
				c2 = c0;  c0 = t;
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
				c1 = (c1 * norm.alpha - norm.mean[1]) / norm.std[1];
				c2 = (c2 * norm.alpha - norm.mean[2]) / norm.std[2];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
				c1 = c1 * norm.alpha + norm.beta;
				c2 = c2 * norm.alpha + norm.beta;
			}

			int area = dst_width * dst_height;
			float* pdst_c0 = dst + dy * dst_width + dx;
			float* pdst_c1 = pdst_c0 + area;
			float* pdst_c2 = pdst_c1 + area;
            float* pdst_c4 = pdst_c2 + area;
			*pdst_c0 = c0;
			*pdst_c1 = c1;
			*pdst_c2 = c2;
            *pdst_c4 = c4;
		}
		else
		{
			float c0, c1;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
				c1 = 0;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float image_const_value[] = { const_value_st };
				float depth_const_value[] = { 0 };

				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;

				float* iv1 = image_const_value;
				float* iv2 = image_const_value;
				float* iv3 = image_const_value;
				float* iv4 = image_const_value;
				float* dv1 = depth_const_value;
				float* dv2 = depth_const_value;
				float* dv3 = depth_const_value;
				float* dv4 = depth_const_value;

				if (y_low >= 0)
				{
					if (x_low >= 0)
					{
						iv1 = src_image + y_low * src_width + x_low * 1;
                        dv1 = src_depth + y_low * src_width + x_low * 1;
					}
					if (x_high < src_width)
					{
						iv2 = src_image + y_low * src_width + x_high * 1;
                        dv2 = src_depth + y_low * src_width + x_high * 1;
					}
				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
					{
						iv3 = src_image + y_high * src_width + x_low * 1;
                        dv3 = src_depth + y_high * src_width + x_low * 1;
					}
					if (x_high < src_width)
					{
						iv4 = src_image + y_high * src_width + x_high * 1;
                        dv4 = src_depth + y_high * src_width + x_high * 1;
					}	
				}
				c0 = w1 * iv1[0] + w2 * iv2[0] + w3 * iv3[0] + w4 * iv4[0];
				c1 = w1 * dv1[0] + w2 * dv2[0] + w3 * dv3[0] + w4 * dv4[0];
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
			}

			int area = dst_width * dst_height;
			float* pdst_c0 = dst + dy * dst_width + dx;
			float* pdst_c1 = pdst_c0 + area;
			*pdst_c0 = c0;
			*pdst_c1 = c1;
		}
	}


	__global__ void warp_affine_bilinear_and_normalize_depth_mix_mask_kernel(float* src_depth, float* src_mask, int src_width, int src_height,
		float* dst, int dst_width, int dst_height, float const_value_st, float* warp_affine_matrix_2_3, Norm norm, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge) return;

		float m_x1 = warp_affine_matrix_2_3[0];
		float m_y1 = warp_affine_matrix_2_3[1];
		float m_z1 = warp_affine_matrix_2_3[2];
		float m_x2 = warp_affine_matrix_2_3[3];
		float m_y2 = warp_affine_matrix_2_3[4];
		float m_z2 = warp_affine_matrix_2_3[5];

		int dx = position % dst_width;
		int dy = position / dst_width;
		float src_x = m_x1 * dx + m_y1 * dy + m_z1;
		float src_y = m_x2 * dx + m_y2 * dy + m_z2;

		float c0;
		float c1;
		
		if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
		{
			// out of range
			c0 = 0;
			c1 = 0;
		}
		else
		{
			int y_low = floorf(src_y);
			int x_low = floorf(src_x);
			int y_high = y_low + 1;
			int x_high = x_low + 1;

			float depth_const_value[] = { 0 };

			float ly = src_y - y_low;
			float lx = src_x - x_low;
			float hy = 1 - ly;
			float hx = 1 - lx;
			float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;
			
			float* dv1 = depth_const_value;
			float* dv2 = depth_const_value;
			float* dv3 = depth_const_value;
			float* dv4 = depth_const_value;
			if (y_low >= 0)
			{
				if (x_low >= 0)
				{
					dv1 = src_depth + y_low * src_width + x_low * 1;
				}
					

				if (x_high < src_width)
				{
					dv2 = src_depth + y_low * src_width + x_high * 1;
				}
			}

			if (y_high < src_height)
			{
				if (x_low >= 0)
				{
					dv3 = src_depth + y_high * src_width + x_low * 1;
				}

				if (x_high < src_width)
				{
					dv4 = src_depth + y_high * src_width + x_high * 1;
				}
			}
			c0 = w1 * dv1[0] + w2 * dv2[0] + w3 * dv3[0] + w4 * dv4[0];

			int sx = (int)roundf(src_x);
			int sy = (int)roundf(src_y);
			if (sx >= 0 && sx < src_width && sy >= 0 && sy < src_height)
			{
				c1 = src_mask[sy * src_width + sx];
			}
			else
			{
				c1 = 0;
			}
		}

		if (norm.type == NormType::MeanStd)
		{
			c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
		}
		else if (norm.type == NormType::AlphaBeta)
		{
			c0 = c0 * norm.alpha + norm.beta;
		}

		int channel = 1;

		float* dst_pixel = dst + dy * dst_width + dx;
		int area = dst_width * dst_height;

		for (int chlId = 0; chlId < channel; ++chlId)
		{
			dst_pixel[area * chlId] = c0;
		}
		dst_pixel[area * channel] = c1;

	}


	__global__ void warp_affine_bilinear_and_normalize_image_mix_depth_mix_mask_kernel(
		float* src_image, int src_line_size, float* src_depth, float* src_mask, int src_width, int src_height,
		float* dst, int dst_width, int dst_height, float const_value_st, float* warp_affine_matrix_2_3, Norm norm, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge)
			return;

		float m_x1 = warp_affine_matrix_2_3[0];
		float m_y1 = warp_affine_matrix_2_3[1];
		float m_z1 = warp_affine_matrix_2_3[2];
		float m_x2 = warp_affine_matrix_2_3[3];
		float m_y2 = warp_affine_matrix_2_3[4];
		float m_z2 = warp_affine_matrix_2_3[5];

		int dx = position % dst_width;
		int dy = position / dst_width;
		float src_x = m_x1 * dx + m_y1 * dy + m_z1;
		float src_y = m_x2 * dx + m_y2 * dy + m_z2;

		int channel = (int)(src_line_size / src_width);

		if (channel == 3)
		{
			float c0, c1, c2, c4, c5;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
				c1 = const_value_st;
				c2 = const_value_st;
				c4 = 0;
                c5 = 0;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float image_const_value[] = { const_value_st, const_value_st, const_value_st };
				float depth_const_value[] = { 0 };

				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;

				float* iv1 = image_const_value;
				float* iv2 = image_const_value;
				float* iv3 = image_const_value;
				float* iv4 = image_const_value;
				float* dv1 = depth_const_value;
				float* dv2 = depth_const_value;
				float* dv3 = depth_const_value;
				float* dv4 = depth_const_value;
				if (y_low >= 0)
				{
					if (x_low >= 0)
					{
						iv1 = src_image + y_low * src_line_size + x_low * 3;
						dv1 = src_depth + y_low * src_width + x_low * 1;
					}

					if (x_high < src_width)
					{
						iv2 = src_image + y_low * src_line_size + x_high * 3;
						dv2 = src_depth + y_low * src_width + x_high * 1;
					}

				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
					{
						iv3 = src_image + y_high * src_line_size + x_low * 3;
						dv3 = src_depth + y_high * src_width + x_low * 1;
					}

					if (x_high < src_width)
					{
						iv4 = src_image + y_high * src_line_size + x_high * 3;
						dv4 = src_depth + y_high * src_width + x_high * 1;
					}
				}

				c0 = w1 * iv1[0] + w2 * iv2[0] + w3 * iv3[0] + w4 * iv4[0];
				c1 = w1 * iv1[1] + w2 * iv2[1] + w3 * iv3[1] + w4 * iv4[1];
				c2 = w1 * iv1[2] + w2 * iv2[2] + w3 * iv3[2] + w4 * iv4[2];
				c4 = w1 * dv1[0] + w2 * dv2[0] + w3 * dv3[0] + w4 * dv4[0];

				int sx = (int)roundf(src_x);
				int sy = (int)roundf(src_y);
				if (sx >= 0 && sx < src_width && sy >= 0 && sy < src_height)
				{
					c5 = src_mask[sy * src_width + sx];
				}
				else
				{
					c5 = 0;
				}
			}

			if (norm.channel_type == ChannelType::Invert)
			{
				float t = c2;
				c2 = c0;  c0 = t;
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
				c1 = (c1 * norm.alpha - norm.mean[1]) / norm.std[1];
				c2 = (c2 * norm.alpha - norm.mean[2]) / norm.std[2];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
				c1 = c1 * norm.alpha + norm.beta;
				c2 = c2 * norm.alpha + norm.beta;
			}

			int area = dst_width * dst_height;
			float* pdst_c0 = dst + dy * dst_width + dx;
			float* pdst_c1 = pdst_c0 + area;
			float* pdst_c2 = pdst_c1 + area;
			float* pdst_c4 = pdst_c2 + area;
            float* pdst_c5 = pdst_c4 + area;
			*pdst_c0 = c0;
			*pdst_c1 = c1;
			*pdst_c2 = c2;
			*pdst_c4 = c4;
            *pdst_c5 = c5;
		}
		else
		{
			float c0, c1, c2;
			if (src_x <= -1 || src_x >= src_width || src_y <= -1 || src_y >= src_height)
			{
				// out of range
				c0 = const_value_st;
				c1 = 0;
				c2 = 0;
			}
			else
			{
				int y_low = floorf(src_y);
				int x_low = floorf(src_x);
				int y_high = y_low + 1;
				int x_high = x_low + 1;

				float image_const_value[] = { const_value_st };
				float depth_const_value[] = { 0 };

				float ly = src_y - y_low;
				float lx = src_x - x_low;
				float hy = 1 - ly;
				float hx = 1 - lx;
				float w1 = hy * hx, w2 = hy * lx, w3 = ly * hx, w4 = ly * lx;

				float* iv1 = image_const_value;
				float* iv2 = image_const_value;
				float* iv3 = image_const_value;
				float* iv4 = image_const_value;
				float* dv1 = depth_const_value;
				float* dv2 = depth_const_value;
				float* dv3 = depth_const_value;
				float* dv4 = depth_const_value;
				if (y_low >= 0)
				{
					if (x_low >= 0)
					{
						iv1 = src_image + y_low * src_width + x_low * 1;
						dv1 = src_depth + y_low * src_width + x_low * 1;
					}
					if (x_high < src_width)
					{
						iv2 = src_image + y_low * src_width + x_high * 1;
						dv2 = src_depth + y_low * src_width + x_high * 1;
					}
				}

				if (y_high < src_height)
				{
					if (x_low >= 0)
					{
						iv3 = src_image + y_high * src_width + x_low * 1;
						dv3 = src_depth + y_high * src_width + x_low * 1;
					}
					if (x_high < src_width)
					{
						iv4 = src_image + y_high * src_width + x_high * 1;
						dv4 = src_depth + y_high * src_width + x_high * 1;
					}
				}
				c0 = w1 * iv1[0] + w2 * iv2[0] + w3 * iv3[0] + w4 * iv4[0];
				c1 = w1 * dv1[0] + w2 * dv2[0] + w3 * dv3[0] + w4 * dv4[0];

				int sx = (int)roundf(src_x);
				int sy = (int)roundf(src_y);
				if (sx >= 0 && sx < src_width && sy >= 0 && sy < src_height)
				{
					c2 = src_mask[sy * src_width + sx];
				}
				else
				{
					c2 = 0;
				}
			}

			if (norm.type == NormType::MeanStd)
			{
				c0 = (c0 * norm.alpha - norm.mean[0]) / norm.std[0];
			}
			else if (norm.type == NormType::AlphaBeta)
			{
				c0 = c0 * norm.alpha + norm.beta;
			}

			int area = dst_width * dst_height;
			float* pdst_c0 = dst + dy * dst_width + dx;
			float* pdst_c1 = pdst_c0 + area;
            float* pdst_c2 = pdst_c1 + area;
			*pdst_c0 = c0;
			*pdst_c1 = c1;
            *pdst_c2 = c2;
		}
	}


    static __device__ uint8_t cast(float value){
        return value < 0 ? 0 : (value > 255 ? 255 : value);
    }


	__global__ void reduceMinMaxKernel(const float* depth, float* max_vals, float* min_vals,
		float lower_limit, float upper_limit, int edge)
	{
		extern __shared__ float sdata[];
		float* smax = sdata;
		float* smin = sdata + blockDim.x;

		int tid = threadIdx.x;
		int i = blockIdx.x * blockDim.x + tid;

		float val = (i < edge) ? depth[i] : -INFINITY;
		float local_max = ((i < edge && isfinite(val) && val >= lower_limit && val <= upper_limit)) ? val : -INFINITY;
		float local_min = ((i < edge && isfinite(val) && val >= lower_limit && val <= upper_limit)) ? val : INFINITY;

		smax[tid] = local_max;
		smin[tid] = local_min;

		__syncthreads();

		for (int s = blockDim.x / 2; s > 0; s >>= 1)
		{
			if (tid < s)
			{
				smax[tid] = fmaxf(smax[tid], smax[tid + s]);
				smin[tid] = fminf(smin[tid], smin[tid + s]);
			}

			__syncthreads();
		}

		if (tid == 0)
		{
			max_vals[blockIdx.x] = smax[0];
			min_vals[blockIdx.x] = smin[0];
		}
	}

	__global__ void clipDepthValueKernel(float* depth, int cols, int rows, float min_val, float max_val, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge)
			return;

		int x = position % cols;
		int y = position / cols;

		float* c1;

		c1 = depth + y * cols + x * 1;

		if (!isfinite(*c1) || *c1 <= min_val || *c1 >= max_val)
		{
			*c1 = min_val;
		}
		if ((max_val - min_val) != 0)
			*c1 = (*c1 - min_val) / (max_val - min_val);
		else
			*c1 = *c1 - min_val;
	}

	__global__ void clipDepthValueWithMaskKernel(float* depth, float* mask, int cols, int rows, float min_val, float max_val, int edge)
	{
		int position = blockDim.x * blockIdx.x + threadIdx.x;
		if (position >= edge)
			return;

		int x = position % cols;
		int y = position / cols;

		float* c1;
		float* c2;

		c1 = depth + y * cols + x * 1;
		c2 = mask + y * cols + x * 1;

		if (!isfinite(*c1) || *c1 < min_val || *c1 > max_val)
		{
			*c1 = min_val;
			*c2 = 0;
		}
		else
		{
			*c2 = 1;
		}

		if ((max_val - min_val) != 0)
			*c1 = (*c1 - min_val) / (max_val - min_val);
		else
			*c1 = *c1 - min_val;
	}


	/////////////////////////////////////////////////////////////////////////

	void warp_affine_bilinear_and_normalize_plane_general(
		float* src, int src_line_size, int src_width, int src_height, float* dst, int dst_width, int dst_height,
		float* matrix_2_3, float const_value, const Norm& norm,
		cudaStream_t stream)
	{
		int jobs = dst_width * dst_height;
		auto grid = CUDATools::grid_dims(jobs);
		auto block = CUDATools::block_dims(jobs);

		checkCudaKernel(warp_affine_bilinear_and_normalize_plane_general_kernel << <grid, block, 0, stream >> > (
			src, src_line_size,
			src_width, src_height, dst,
			dst_width, dst_height, const_value, matrix_2_3, norm, jobs
			));
	}


	void warp_affine_bilinear_and_normalize_image_mix_depth(
		float* src_image, int src_line_size, float* src_depth, int src_width, int src_height, float* dst, int dst_width, int dst_height,
		float* matrix_2_3, float const_value, const Norm& norm, cudaStream_t stream)
	{
		int jobs = dst_width * dst_height;
		auto grid = CUDATools::grid_dims(jobs);
		auto block = CUDATools::block_dims(jobs);

		checkCudaKernel(warp_affine_bilinear_and_normalize_image_mix_depth_kernel << <grid, block, 0, stream >> > (
			src_image, src_line_size, src_depth, src_width, src_height, 
			dst, dst_width, dst_height, const_value, matrix_2_3, norm, jobs));
	}


	void warp_affine_bilinear_and_normalize_depth_mix_mask(
		float* src_depth, float* src_mask, int src_width, int src_height, float* dst, int dst_width, int dst_height,
		float* matrix_2_3, float const_value, const Norm& norm, cudaStream_t stream)
	{
		int jobs = dst_width * dst_height;
		auto grid = CUDATools::grid_dims(jobs);
		auto block = CUDATools::block_dims(jobs);

		checkCudaKernel(warp_affine_bilinear_and_normalize_depth_mix_mask_kernel << <grid, block, 0, stream >> > (
			src_depth, src_mask, src_width, src_height, dst, dst_width, dst_height, const_value, matrix_2_3, norm, jobs));
	}


	void warp_affine_bilinear_and_normalize_image_mix_depth_mix_mask(
		float* src_image, int src_line_size, float* src_depth, float* src_mask, int src_width, int src_height,
		float* dst, int dst_width, int dst_height,
		float* matrix_2_3, float const_value, const Norm& norm,
		cudaStream_t stream)
	{
		int jobs = dst_width * dst_height;
		auto grid = CUDATools::grid_dims(jobs);
		auto block = CUDATools::block_dims(jobs);

		checkCudaKernel(warp_affine_bilinear_and_normalize_image_mix_depth_mix_mask_kernel << <grid, block, 0, stream >> > (
			src_image, src_line_size, src_depth, src_mask, src_width, src_height, dst, dst_width, dst_height, const_value, matrix_2_3, norm, jobs));
	}


	void resize_bilinear_and_normalize(
		uint8_t* src, int src_line_size, int src_width, int src_height, float* dst, int dst_width, int dst_height,
		const Norm& norm,
		cudaStream_t stream) {
		
		int jobs   = dst_width * dst_height;
		auto grid  = CUDATools::grid_dims(jobs);
		auto block = CUDATools::block_dims(jobs);
		
		checkCudaKernel(resize_bilinear_and_normalize_kernel << <grid, block, 0, stream >> > (
			src, src_line_size,
			src_width, src_height, dst,
			dst_width, dst_height, src_width/(float)dst_width, src_height/(float)dst_height, norm, jobs
		));
	}


	void clip_gray_value(float* src, int cols, int rows, float lower_limit, float upper_limit,
		float& min_val, float& max_val, cudaStream_t stream)
	{
		int size = rows * cols;

		float* d_max_blocks, * d_min_blocks;
		const int blockSize = 256;
		int gridSize = (size + blockSize - 1) / blockSize;

		checkCudaKernel(cudaMalloc(&d_max_blocks, gridSize * sizeof(float)));
		checkCudaKernel(cudaMalloc(&d_min_blocks, gridSize * sizeof(float)));

		checkCudaKernel(reduceMinMaxKernel << <gridSize, blockSize, 2 * blockSize * sizeof(float), stream >> > (
			src, d_max_blocks, d_min_blocks, lower_limit, upper_limit, size));

		float* h_max_blocks = new float[gridSize];
		float* h_min_blocks = new float[gridSize];
		checkCudaKernel(cudaMemcpy(h_max_blocks, d_max_blocks, gridSize * sizeof(float), cudaMemcpyDeviceToHost));
		checkCudaKernel(cudaMemcpy(h_min_blocks, d_min_blocks, gridSize * sizeof(float), cudaMemcpyDeviceToHost));

		max_val = -INFINITY;
		min_val = INFINITY;
		for (int i = 0; i < gridSize; ++i)
		{
			if (h_max_blocks[i] > max_val)
				max_val = h_max_blocks[i];
			if (h_min_blocks[i] < min_val)
				min_val = h_min_blocks[i];
		}

		if (max_val == -INFINITY || min_val == INFINITY)
		{
			max_val = min_val = 0.0f;
		}

		auto grid = CUDATools::grid_dims(size);
		auto block = CUDATools::block_dims(size);
		checkCudaKernel(clipDepthValueKernel << <grid, block, 0, stream >> > (src, cols, rows, min_val, max_val, size));

		delete[] h_max_blocks;
		delete[] h_min_blocks;
		checkCudaKernel(cudaFree(d_max_blocks));
		checkCudaKernel(cudaFree(d_min_blocks));
	}


	void clip_gray_value_with_mask(float* src, float* src_mask, int cols, int rows, float lower_limit, float upper_limit,
		float& min_val, float& max_val, cudaStream_t stream)
	{
		int size = rows * cols;

		float* d_max_blocks, * d_min_blocks;
		const int blockSize = 256;
		int gridSize = (size + blockSize - 1) / blockSize;

		checkCudaKernel(cudaMalloc(&d_max_blocks, gridSize * sizeof(float)));
		checkCudaKernel(cudaMalloc(&d_min_blocks, gridSize * sizeof(float)));

		checkCudaKernel(reduceMinMaxKernel << <gridSize, blockSize, 2 * blockSize * sizeof(float), stream >> > (
			src, d_max_blocks, d_min_blocks, lower_limit, upper_limit, size));

		float* h_max_blocks = new float[gridSize];
		float* h_min_blocks = new float[gridSize];
		checkCudaKernel(cudaMemcpy(h_max_blocks, d_max_blocks, gridSize * sizeof(float), cudaMemcpyDeviceToHost));
		checkCudaKernel(cudaMemcpy(h_min_blocks, d_min_blocks, gridSize * sizeof(float), cudaMemcpyDeviceToHost));

		max_val = -INFINITY;
		min_val = INFINITY;
		for (int i = 0; i < gridSize; ++i)
		{
			if (h_max_blocks[i] > max_val)
				max_val = h_max_blocks[i];
			if (h_min_blocks[i] < min_val)
				min_val = h_min_blocks[i];
		}

		if (max_val == -INFINITY || min_val == INFINITY)
		{
			max_val = min_val = 0.0f;
		}

		auto grid = CUDATools::grid_dims(size);
		auto block = CUDATools::block_dims(size);
		checkCudaKernel(clipDepthValueWithMaskKernel << <grid, block, 0, stream >> > (src, src_mask, cols, rows, min_val, max_val, size));

		delete[] h_max_blocks;
		delete[] h_min_blocks;
		checkCudaKernel(cudaFree(d_max_blocks));
		checkCudaKernel(cudaFree(d_min_blocks));
	}


};
